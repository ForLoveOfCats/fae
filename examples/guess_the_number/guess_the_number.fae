import fae.libc.rand
import fae.io.file.File

fn main() {
	play_round()
}

fn seed_rand() {
	import fae.libc.TimeT, srand, time
	let null = null_pointer<TimeT>()
	srand(seed: time(null).time_t.(u32))
}

fn play_round() {
	seed_rand()
	let number = rand().(i64) % 100
	println("Guess the random number between 1 and 100")

	mut stdin = File.new_stdin()
	mut reader = stdin.lines_reader()
	defer reader.free()

	while true {
		print(">>> ")
		let line = reader.next_str().unwrap()

		if line.parse_i64() is guess: Some {
			println(f"You guessed {guess}")

			if guess < number => println("Too low")
			else if guess > number  => println("Too high")
			else {
				println("That was the number!")
				break
			}
		}
	}
}
